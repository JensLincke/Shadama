
<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>Shadama2 5</title>
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="shadama.js" type="text/javascript"></script>

        <script id="forward.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_position;

uniform vec2 u_resolution;
uniform sampler2D u_position;
uniform sampler2D u_direction;
uniform float u_amount;

out vec4 vColor;

vec2 decodePos(vec4 c) {
    return vec2(c.r * 256.0 + c.g, c.b * 256.0 + c.a);
}

vec2 decodeDir(vec4 c) {
    const float PI = 3.1415926535897932384626433832795;
    float v = (c.r * 256.0 * 256.0) + (c.g * 256.0) + c.b;
    float p = (PI * 2.0) / 65536.0 * v;
    return vec2(cos(p), sin(p));
}

vec4 encodePos(vec2 pos) {
    // (0-256.0, 0-256.0)
    return vec4(mod(pos.x, 256.0) / 256.0, mod(pos.x, 1.0),
	 mod(pos.y, 256.0) / 256.0, mod(pos.y, 1.0));
}

void main(void) {
    vec2 zeroToOne = a_position / u_resolution;
    vec2 zeroToTwo = zeroToOne * 2.0;
    vec2 clipSpace = zeroToTwo - 1.0;
    gl_Position = vec4(clipSpace, 0, 1);

    vec4 pos = texture(u_position, zeroToOne);  //encoded
    vec4 dir = texture(u_direction, zeroToOne);
    vec2 dPos = decodePos(pos);         // (0-256.0, 0-256.0)
    vec2 dDir = decodeDir(dir);  // normalized direction vector
    vec2 newPos = dPos + (dDir * u_amount);
    vColor = encodePos(newPos);   // (0-1.0, 0-1.0, 0-1.0, 0-1.0)
    gl_PointSize = 1.0;
}
        </script>
        <script id="forward.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec4 vColor;
out vec4 outColor;

void main(void) {
    outColor = vColor;
}
        </script>

        <script id="renderBreed.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_position;

uniform vec2 u_resolution;
uniform sampler2D u_position;

vec2 decodePos(vec4 c) {
    return vec2(c.r * 256.0 + c.g, c.b * 256.0 + c.a);
}

void main(void) {
    vec2 zeroToOne = a_position / u_resolution;

    vec4 pos = texture(u_position, zeroToOne);  //encoded
    vec2 dPos = decodePos(pos);   // (0-256.0, 0-256.0)
    vec2 normPos = dPos / 256.0;  // (0-1.0, 0-1.0)
    vec2 normPos2 = normPos * 2.0;  // (0-2.0, 0-2.0)
    vec2 clipPos = normPos2 - 1.0;  // (-1.0-1.0, -1.0-1.0)

    gl_Position = vec4(clipPos, 0, 1.0);
    gl_PointSize = 1.0;
}
        </script>

        <script id="renderBreed.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

uniform    vec4 u_color;

out vec4 outColor;

void main(void) {
  outColor = u_color;
}
        </script>
	</head>
	<body>
		<canvas id="canvas"></canvas>
		<canvas id="debugCanvas1"></canvas>
		<div id="readout"</div>
	</body>
</html>
