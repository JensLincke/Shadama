<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
        <title>Shadama2</title>
        <link href="shadama.css" rel="stylesheet">
        <link rel="stylesheet" href="codemirror/codemirror.css">
        <script src="codemirror/codemirror.js"></script>
        <script src="ohm/ohm.min.js"></script>
        <script src="shadama-lang.js"></script>
        <script src="shadama.js" type="text/javascript"></script>

        <script type="text/ohm-js">
Shadama {
  TopLevel
    = (Breed | Patch | Script)* Setup? Loop?

  Breed = breed ident "(" Formals ")"
  Patch = patch ident "(" Formals ")"
  Script = def ident "." ident "(" Formals ")" Block

  Setup = setup Block

  Loop = loop Block

  Formals
    = ident ("," ident)* -- list
    | empty

  Block = "{" StatementList "}"

  StatementList = Statement*

  Statement
    = Block
    | VariableStatement
    | AssignmentStatement
    | ExpressionStatement
    | IfStatement
    | ExpressionStatement

  VariableStatement = var VariableDeclaration ";"
  VariableDeclaration = ident Initialiser?
  Initialiser = "=" Expression

  ExpressionStatement = Expression ";"
  IfStatement = if "(" Expression ")" Statement (else Statement)?

  AssignmentStatement
    = LeftHandSideExpression "=" Expression ";"

  LeftHandSideExpression
    = ident "." ident -- field
    | ident

  Expression = EqualityExpression

  EqualityExpression
    = EqualityExpression "==" RelationalExpression  -- equal
    | EqualityExpression "!=" RelationalExpression  -- notEqual
    | RelationalExpression

  RelationalExpression
    = RelationalExpression "<" AddExpression           -- lt
    | RelationalExpression ">" AddExpression           -- gt
    | RelationalExpression "<=" AddExpression          -- le
    | RelationalExpression ">=" AddExpression          -- ge
    | AddExpression

  AddExpression
    = AddExpression "+" MulExpression  -- plus
    | AddExpression "-" MulExpression -- minus
    | MulExpression

  MulExpression
    = MulExpression "*" PrimExpression  -- times
    | MulExpression "/" PrimExpression  -- divide
    | UnaryExpression

  UnaryExpression
    = "+" PrimExpression -- plus
    | "-" PrimExpression -- minus
    | PrimExpression

  PrimExpression
    = "(" Expression ")"  -- paren
    | PrimitiveCall
    | MethodCall
    | ident "." ident     -- field
    | ident               -- variable
    | string              -- string
    | number              -- number

  PrimitiveCall
    = ident "(" Actuals ")"

  MethodCall
    = ident "." ident "(" Actuals ")"

  Actuals
    = Expression ("," Expression)* -- list
    | empty

  ident
    = letter (alnum | "_")*

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole

  string = "\"" doubleStringCharacter* "\""

  doubleStringCharacter 
    = "\\" any           -- escaped
    | ~"\"" any          -- nonEscaped

  identifierStart = letter | "_"
  identifierPart = identifierStart | digit

  var = "var" ~identifierPart
  if = "if" ~identifierPart
  breed = "breed" ~identifierPart
  patch = "patch" ~identifierPart
  else = "else" ~identifierPart
  def = "def" ~identifierPart
  this = "this" ~identifierPart
  self = "self" ~identifierPart
  loop = "loop" ~identifierPart
  setup = "setup" ~identifierPart

  empty =
}
    </script>

        <script id="drawBreed.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_x;
uniform sampler2D u_y;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 v_color;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    float x = texelFetch(u_x, ivec2(a_index), 0).r;
    float y = texelFetch(u_y, ivec2(a_index), 0).r;
    vec2 dPos = vec2(x, y);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)
    gl_Position = vec4(clipPos, 0, 1.0);

    float r = texelFetch(u_r, ivec2(a_index), 0).r;
    float g = texelFetch(u_g, ivec2(a_index), 0).r;
    float b = texelFetch(u_b, ivec2(a_index), 0).r;
    float a = texelFetch(u_a, ivec2(a_index), 0).r;
    v_color = vec4(r, g, b, a);
    gl_PointSize = 1.0;
}
        </script>

        <script id="drawBreed.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec4 v_color;

out vec4 fragColor;

void main(void) {
  fragColor = v_color;
}
        </script>
        <script id="setPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)

    gl_Position = vec4(clipPos, 0, 1.0);
    gl_PointSize = 1.0;
}
        </script>

        <script id="setPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform vec4 u_value;
uniform int u_type; // 0 = Number, 1 = Color

out vec4 fragColor;

void main(void) {
  if (u_type == 0) {
    fragColor = u_value;
  } else if(u_type == 1) {
    fragColor = u_value;
  } else {
    discard;
  }
}
        </script>
        <script id="getPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;

out vec2 v_coord;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)

    gl_Position = vec4(oneToOne, 0, 1);
    v_coord = dPos;
    gl_PointSize = 1.0;
}
        </script>

        <script id="getPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;
uniform int u_type; // 0 = Number, 1 = Color

in vec2 v_coord;

out vec4 fragColor;

void main(void) {
  if (u_type == 0) {
    fragColor = texelFetch(u_value, ivec2(v_coord), 0);
  } else if(u_type == 1) {
    fragColor = texelFetch(u_value, ivec2(v_coord), 0);
  } else {
    discard;
  }
}
        </script>

        <script id="drawPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="drawPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float r = texelFetch(u_r, fc, 0).r;
  float g = texelFetch(u_g, fc, 0).r;
  float b = texelFetch(u_b, fc, 0).r;
  float a = texelFetch(u_a, fc, 0).r;
  fragColor = vec4(r, g, b, a);
}
        </script>
        <script id="diffusePatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="diffusePatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform vec2      u_resolution;
uniform sampler2D u_value;

const float weight[9] = float[9](
    0.077847, 0.123317, 0.077847,
    0.123317, 0.195346, 0.123317,
    0.077847, 0.123317, 0.077847
);

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  vec4 v  = texelFetch(u_value, fc + ivec2(-1, -1), 0) * weight[0];
       v += texelFetch(u_value, fc + ivec2(-1,  0), 0) * weight[1];
       v += texelFetch(u_value, fc + ivec2(-1,  1), 0) * weight[2];
       v += texelFetch(u_value, fc + ivec2( 0, -1), 0) * weight[3];
       v += texelFetch(u_value, fc + ivec2( 0,  0), 0) * weight[4];
       v += texelFetch(u_value, fc + ivec2( 0,  1), 0) * weight[5];
       v += texelFetch(u_value, fc + ivec2( 1, -1), 0) * weight[6];
       v += texelFetch(u_value, fc + ivec2( 1,  0), 0) * weight[7];
       v += texelFetch(u_value, fc + ivec2( 1,  1), 0) * weight[8];
       fragColor = v.r <= (1.0/256.0) ? vec4(0.0) : v;
}
        </script>
        <script id="debugPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="debugPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

out vec4 fragColor;

void main(void) {
    ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
    fragColor = texelFetch(u_value, fc, 0);
}
        </script>

        <script id="debugBreed.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform float u_particleLength;
uniform sampler2D u_value;

out vec4 v_color;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)

    v_color = texelFetch(u_value, ivec2(a_index), 0);
    gl_Position = vec4(oneToOne, 0, 1.0);
    gl_PointSize = 1.0;
}
        </script>

        <script id="debugBreed.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec4 v_color;

out vec4 fragColor;

void main(void) {
    fragColor = v_color;
}
        </script>

        <script id="forward.shadama" type="x-shadama">
breed Turtle (x, y, dirX, dirY, r, g, b, a)
breed Filler (x, y, dirX, dirY, r, g, b, a)
patch Patch (r, g, b, a)

def breed.forward(n, left, right, top, bottom) {
  var dx = this.dirX;
  var dy = this.dirY;
  var maybeX = this.x + dx * n;
  var maybeY = this.y + dy * n;
           
  if (maybeX < 0.0) {
    if (left == 1.0) {
      maybeX = 0.0 - maybeX;
      dx = -dx;
    } else if (left == 2.0) {
      maybeX = 0.0;
    } else {
      maybeX = mod(maybeX, u_resolution.x);
    }
  }
  if (maybeX > u_resolution.x) {
    if (right == 1.0) {
      maybeX = u_resolution.x - (maybeX - u_resolution.x);
      dx = -dx;
    } else if (right == 2.0) {
      maybeX = u_resolution.x;
    } else {
      maybeX = mod(maybeX, u_resolution.x);
    }
  }
  if (maybeY < 0.0) {
    if (bottom == 1.0) {
      maybeY = 0.0 - maybeY;
      dy = -dy;
    } else if (bottom == 2.0) {
      maybeY = 0.0;
    } else {
      maybeY = mod(maybeY, u_resolution.y);
    }
  }
  if (maybeY > u_resolution.y) {
    if (top == 1.0) {
      maybeY = u_resolution.y - (maybeY - u_resolution.y);
      dy = -dy;
    } else if (top == 2.0) {
      maybeY = u_resolution.y;
    } else {
      maybeY = mod(maybeY, u_resolution.y);
    }
  }
  this.dirX = dx;
  this.dirY = dy;
  this.x = maybeX;
  this.y = maybeY;
}

def breed.turn(d) {
  var c = cos(d);
  var s = sin(d);
  this.dirX = c * this.dirX + (0.0 - s) * this.dirY;
  this.dirY = s * this.dirX + c * this.dirY;
}

def breed.setTrail(patch, value) {
  patch.r = this.x / 256.0;
  patch.g = this.y / 256.0;
  patch.b = 1.0 - (value / 256.0);
  patch.a = 1.0;
}

def breed.dropX(patch) {
  patch.r = this.x / 256.0;
  patch.g = this.x / 256.0;
  patch.b = 0.0;
  patch.a = this.x / 256.0;
}

def breed.setColor() {
  this.r = this.x / 256.0;
  this.g = this.y / 256.0;
  this.b = 0.0;
  this.a = 0.5;
}

setup {
  Turtle.setCount(100000);
  Turtle.fillRandom("x", 0, 256);
  Turtle.fillRandom("y", 0, 256);
  Turtle.fillRandomDir("dirX", "dirY");
  Filler.fillSpace("x", "y", 256, 256);
  Turtle.setColor();
}

loop {
  Display.clear();
  Turtle.forward(1.5, 1, 0, 1, 1);
  Turtle.turn(0.05);
  Turtle.setTrail(Patch, 1.0);
  Patch.draw();
  Turtle.draw();
}
</script>

<style>
#code {
  width: 800px;
  height: 800px;
};
</style>

     
        </head>
        <body>
                <div>
                   <div style="float: left">
                     <canvas id="canvas"></canvas>
                   </div>
                   <textarea style="float: left" id="code"></textarea>
                </div>
                <br>
                <button style="float: left" type="button" onclick="updateCode()">Load</button>
                <div style="clear: left" id="readout"></div>
                <canvas id="debugCanvas1"></canvas>
        </body>
</html>
