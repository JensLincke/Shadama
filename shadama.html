<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
        <title>Shadama2</title>
        <link href="shadama.css" rel="stylesheet">
        <link rel="stylesheet" href="codemirror/codemirror.css">
        <script src="codemirror/codemirror.js"></script>
        <script src="codemirror/clike.js"></script>
        <script src="ohm/ohm.min.js"></script>
        <script src="shadama-lang.js"></script>
        <script src="shadama.js" type="text/javascript"></script>
        <script src="shadama-tests.js" type="text/javascript"></script>
        <script id="copy.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="copy.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_value;

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  fragColor = texelFetch(u_value, fc, 0).r;
}
        </script>


        <script id="drawBreed.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_x;
uniform sampler2D u_y;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 v_color;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    float x = texelFetch(u_x, ivec2(a_index), 0).r;
    float y = texelFetch(u_y, ivec2(a_index), 0).r;
    vec2 dPos = vec2(x, y);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)
    gl_Position = vec4(clipPos, 0, 1.0);

    float r = texelFetch(u_r, ivec2(a_index), 0).r;
    float g = texelFetch(u_g, ivec2(a_index), 0).r;
    float b = texelFetch(u_b, ivec2(a_index), 0).r;
    float a = texelFetch(u_a, ivec2(a_index), 0).r;
    v_color = vec4(r, g, b, a);
    gl_PointSize = 1.0;
}
        </script>

        <script id="drawBreed.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec4 v_color;

out vec4 fragColor;

void main(void) {
  fragColor = v_color;
}
        </script>

        <script id="drawPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="drawPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float r = texelFetch(u_r, fc, 0).r;
  float g = texelFetch(u_g, fc, 0).r;
  float b = texelFetch(u_b, fc, 0).r;
  float a = texelFetch(u_a, fc, 0).r;
  fragColor = vec4(r, g, b, a);
}
        </script>
        <script id="diffusePatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="diffusePatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

const float weight[9] = float[9](
    0.077847, 0.123317, 0.077847,
    0.123317, 0.195346, 0.123317,
    0.077847, 0.123317, 0.077847
);

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float v;
  v = texelFetch(u_value, fc + ivec2(-1, -1), 0).r * weight[0];
  v += texelFetch(u_value, fc + ivec2(-1,  0), 0).r * weight[1];
  v += texelFetch(u_value, fc + ivec2(-1,  1), 0).r * weight[2];
  v += texelFetch(u_value, fc + ivec2( 0, -1), 0).r * weight[3];
  v += texelFetch(u_value, fc + ivec2( 0,  0), 0).r * weight[4];
  v += texelFetch(u_value, fc + ivec2( 0,  1), 0).r * weight[5];
  v += texelFetch(u_value, fc + ivec2( 1, -1), 0).r * weight[6];
  v += texelFetch(u_value, fc + ivec2( 1,  0), 0).r * weight[7];
  v += texelFetch(u_value, fc + ivec2( 1,  1), 0).r * weight[8];
  v = v <= (1.0/256.0) ? 0.0 : v;
  fragColor = v;
}
        </script>

        <script id="debugPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="debugPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

out vec4 fragColor;

void main(void) {
    ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
    fragColor = texelFetch(u_value, fc, 0);
}
        </script>

        <script id="debugFrame.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;
uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_this_r;
uniform sampler2D u_this_g;
uniform sampler2D u_this_b;
uniform sampler2D u_this_a;
out float v_this_r;
out float v_this_g;
out float v_this_b;
out float v_this_a;
void main() {

  vec2 oneToOne = (a_index / u_particleLength) * 2.0 - 1.0;
  v_this_r = texelFetch(u_this_r, ivec2(a_index), 0).r;
  v_this_g = texelFetch(u_this_g, ivec2(a_index), 0).r;
  v_this_b = texelFetch(u_this_b, ivec2(a_index), 0).r;
  v_this_a = texelFetch(u_this_a, ivec2(a_index), 0).r;
  v_this_r = 1.0;
  v_this_g = 0.0;
  v_this_b = 1.0;
  v_this_a = 1.0;

  gl_Position = vec4(oneToOne, 0.0, 1.0);
  gl_PointSize = 1.0;
}

        </script>
        <script id="debugFrame.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
in float v_this_r;
in float v_this_g;
in float v_this_b;
in float v_this_a;
layout (location = 0) out float o_this_r;
layout (location = 1) out float o_this_g;
layout (location = 2) out float o_this_b;
layout (location = 3) out float o_this_a;
void main() {
  o_this_r = v_this_r;
  o_this_g = v_this_g;
  o_this_b = v_this_b;
  o_this_a = v_this_a;
}
        </script>
        <script id="forward.shadama" type="x-shadama">
program "Bounce"

breed Turtle (x, y, dx, dy, r, g, b, a)
breed Filler (x, y)
patch Field (nx, ny, r, g, b, a)

def setColor() {
  this.r = this.x / 512.0;
  this.g = this.y / 512.0;
  this.b = 0.0;
  this.a = 1.0;
}

def clear(field) {
  field.r = 0.0;
  field.g = 0.0;
  field.b = 0.0;
  field.a = 0.0;
  field.nx = 0.0;
  field.ny = 0.0;
}

def fillCircle(cx, cy, r, field) {
  var dx = this.x - cx;
  var dy = this.y - cy;
  var dr = sqrt(dx * dx + dy * dy);
  if (dr < r) {
    field.r = 0.2;
    field.g = 0.2;
    field.b = 0.8;
    field.a = 1.0;
    field.nx = dx / r;
    field.ny = dy / r;
  }
}

def zeroDir() {
  this.dx = 0.0;
  this.dy = 0.0;
}
 
def bounce(field) {
  var nx = field.nx;
  var ny = field.ny;
  var dx = this.dx;
  var dy = this.dy - 0.01;
  var dot = dx * nx + dy * ny;
  var rx = dx;
  var ry = dy;
  var origV = sqrt(dx * dx + dy * dy);

  if (dot < 0.0) {
    rx = dx - 2.0 * dot * nx;
    ry = dy - 2.0 * dot * ny;
    var norm = sqrt(rx * rx + ry * ry);
    rx = rx / (norm / origV);
    ry = ry / (norm / origV);
  }

  var newX = this.x + dx;
  var newY = this.y + dy;

  if (newX < 0.0) {
    newX = -newX;
    rx = -rx * 0.9;
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x);
    rx = -rx * 0.9;
  }
  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y);
    ry = -0.1;
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y);
    ry = -ry;
  }

  this.x = newX;
  this.y = newY;
  this.dx = rx;
  this.dy = ry;
}

static setup() {
  Filler.fillSpace("x", "y", 512, 512);
  Turtle.setCount(300000);
  Turtle.fillRandom("x", 0, 512);
  Turtle.fillRandom("y", 256, 512);
  Turtle.fillRandomDir("dx", "dy");
  Turtle.setColor();
}

static loop(env) {
  Filler.clear(Field);
  Filler.fillCircle(mousemove.x, mousemove.y, 25, Field);
  Filler.fillCircle(300, 95, 25, Field);
  Turtle.bounce(Field);
  Field.draw();
  Turtle.draw();
}
</script>

<script id="degauss.shadama" type="x-shadama">
program "DeGauss"
breed Pict (x, y, r, g, b, a, origX, origY, origR, origG, origB, origA)
breed Filler (x, y, r, g, b, a)
breed Button (x, y, r, g, b, a)
patch Blur (level)
  
def move(x, y) {
  this.x = this.x + x;
  this.y = this.y + y;
}

def remember1() {
  this.origX = this.x;
  this.origY = this.y;
  this.origR = this.r;
  this.origG = this.g;
  this.origB = this.b;
  this.origA = this.a;
}

def storeBlur(blur) {
  blur.level = this.r;
}

def restore() {
  this.x = this.origX;
  this.y = this.origY;
  this.r = this.origR;
  this.g = this.origG;
  this.b = this.origB;
  this.a = this.origA;
}

def colorize2(blur) {
  var p = 1.0 - blur.level;
   this.r = this.origR * p + this.origG * (1.0 - p);
   this.g = this.origG * p;
   this.b = this.origB * p + this.origG * (1.0 - p);
   this.a = this.origA;
}

def colorize3() {
  this.x = this.origX;
  this.y = this.origY;
}

def degauss(step, begin) {
  var x = step * 50.0;
  var off = step - begin;
  if (step - begin > 0.4) {
    var arg = -2.4 - off;
    x = x + (this.origY - 128) / 100.0 + (this.origX -128) / 100.0 * sin(step) * 3;
    var c = cos(x) * exp(arg);
    var s = sin(x) * exp(arg);
  
    this.x = this.origX + (this.origX - 128) * c;
    this.y = this.origY + (this.origY - 128) * s;
  }
}

def degaussColor(step, begin) {
  this.r = this.r - (this.r - this.origR) * 0.01;
  this.g = this.g - (this.g - this.origG) * 0.01;
}
  
static setup() {
  Pict.fillImage("x", "y", "r", "g", "b", "a", windows);
  Pict.remember1();
  Filler.fillImage("x", "y", "r", "g", "b", "a", blurBig);
  Filler.storeBlur(Blur);
  Button.fillImage("x", "y", "r", "g", "b", "a", button);
  Button.move(760, 160);

  Pict.colorize2(Blur);
  Pict.draw();
  var begin = -1;
  var showButton = 0;
}

static rrestore() {
  Pict.restore();
}

static loop() {
  if (mousemove.x > 950) {
    if (showButton == 0) {
      showButton = 1;
    }
    if (mousemove.y < 200) {
      if (begin == -1) {
        begin = time;
        Display.playSound("degauss");
      }
    }
  }
  if (begin != -1) {
    Pict.degauss(time, begin);
    Pict.degaussColor(time, begin);
  }
  Pict.draw();
  if (showButton == 1) {
     Button.draw();
  }
}
</script>

        </head>
        <body>
          <h1 id="bigTitle">Shadama</h1>
                <div style="float: left">
                      <canvas id="shadamaCanvas"></canvas>
                </div>
                <textarea style="float: left" id="code"></textarea>
                <div style="float: left" id="controlBox">
                  <select id="myDropdown" class="dropdown-content">
                  </select>
                  <hr>
                  <div id="watcherList"></div>
                  <hr>
                  <div id="envList"></div>
                </div>
                <br>
                <div style="clear: left" id="readout"></div>
		<button onclick="goFullScreen()">Full Screen</button>
                <!-- <canvas id="debugCanvas1"></canvas> -->
        </body>
</html>
