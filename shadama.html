
<html>
	<head>
		<meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
		<title>Shadama2</title>
		<link href="shadama.css" rel="stylesheet">
		<script src="minMatrixb.js" type="text/javascript"></script>
		<script src="shadama.js" type="text/javascript"></script>

        <script id="forward.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;
uniform float u_amount;

out vec4 vColor;

vec4 encodeVal(vec2 pos, vec2 dir) {
    return vec4(mod(pos, u_resolution), dir);
}

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;
    gl_Position = vec4(oneToOne, 0, 1);

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);
    vec2 dDir = val.ba; // decodeDir(val);
    vec2 newPos = dPos + (dDir * u_amount);
    vColor = encodeVal(newPos, dDir);
    gl_PointSize = 1.0;
}
        </script>
        <script id="forward.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec4 vColor;

out vec4 outColor;

void main(void) {
    outColor = vColor;
}
        </script>

        <script id="turn.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;
uniform mat2 u_rot;

out vec4 vColor;

vec4 encodeVal(vec2 pos, vec2 dir) {
    return vec4(pos, dir);
}

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;
    gl_Position = vec4(oneToOne, 0, 1);

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);
    vec2 dDir = val.ba; // decodeDir(val);

    vec2 newDir = u_rot * dDir;
    vColor = encodeVal(dPos, newDir);
    gl_PointSize = 1.0;
}
        </script>
        <script id="turn.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec4 vColor;

out vec4 fragColor;

void main(void) {
    fragColor = vColor;
}
        </script>

        <script id="drawBreed.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;

out vec2 v_coord;

vec2 decodePos(vec4 c) {
    return c.rg;
}

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = decodePos(val);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)

    v_coord = a_index;

    gl_Position = vec4(clipPos, 0, 1.0);
    gl_PointSize = 1.0;
}
        </script>

        <script id="drawBreed.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

uniform sampler2D u_color;

in vec2 v_coord;

out vec4 fragColor;

void main(void) {
  fragColor = texelFetch(u_color, ivec2(v_coord), 0);
}
        </script>
        <script id="setPatch.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)

    gl_Position = vec4(clipPos, 0, 1.0);
    gl_PointSize = 1.0;
}
        </script>

        <script id="setPatch.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform vec4 u_value;
uniform int u_type; // 0 = Number, 1 = Color

out vec4 fragColor;

void main(void) {
  if (u_type == 0) {
    fragColor = u_value;
  } else if(u_type == 1) {
    fragColor = u_value;
  } else {
    discard;
  }
}
        </script>
        <script id="getPatch.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;

out vec2 v_coord;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)

    gl_Position = vec4(oneToOne, 0, 1);
    v_coord = dPos;
    gl_PointSize = 1.0;
}
        </script>

        <script id="getPatch.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform sampler2D u_value;
uniform int u_type; // 0 = Number, 1 = Color

in vec2 v_coord;

out vec4 fragColor;

void main(void) {
  if (u_type == 0) {
    fragColor = texelFetch(u_value, ivec2(v_coord), 0);
  } else if(u_type == 1) {
    fragColor = texelFetch(u_value, ivec2(v_coord), 0);
  } else {
    discard;
  }
}
        </script>
        <script id="bounceIf.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_position;
uniform sampler2D u_buffer;

out vec4 vColor;

vec4 encodeVal(vec2 pos, vec2 dir) {
    return vec4(mod(pos, u_resolution), dir);
}

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    vec2 oneToOne = zeroToOne * 2.0 - 1.0;
    gl_Position = vec4(oneToOne, 0, 1);

    vec4 val = texelFetch(u_position, ivec2(a_index), 0);
    vec2 dPos = val.rg; // decodePos(val);
    vec2 dDir = val.ba; // decodeDir(val);

    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)

    float v = texelFetch(u_buffer, ivec2(dPos), 0).r;

    vec2 newDir;
    vec2 newPos;
    if (v > 0.25) {
      newDir = mat2(-1.0, 0.0, 0.0, -1.0) * dDir;
      //newPos = dPos + newDir * 1.4;
    } else {
      newDir = dDir;
      //newPos = dPos;
    }
    vColor = encodeVal(dPos, newDir);
    gl_PointSize = 1.0;
}
        </script>
        <script id="bounceIf.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;

in vec4 vColor;

out vec4 fragColor;

void main(void) {
    fragColor = vColor;
}
        </script>
        <script id="drawPatch.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_position;

out vec2 v_coord;

void main(void) {
    v_coord = (a_position / 2.0) + 0.5;
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="drawPatch.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform sampler2D u_value;
uniform int u_type;  // 0 = Number, 1 = Color

in vec2 v_coord;

out vec4 fragColor;

void main(void) {
  if (u_type == 0) {
    fragColor = texture(u_value, v_coord);
  } else if(u_type == 1) {
    fragColor = texture(u_value, v_coord);
  } else {
    discard;
  }
}
        </script>
        <script id="diffusePatch.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="diffusePatch.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform vec2      u_resolution;
uniform sampler2D u_value;

const float weight[9] = float[9](
    0.077847, 0.123317, 0.077847,
    0.123317, 0.195346, 0.123317,
    0.077847, 0.123317, 0.077847);

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  vec4 v  = texelFetch(u_value, fc + ivec2(-1, -1), 0) * weight[0];
       v += texelFetch(u_value, fc + ivec2(-1,  0), 0) * weight[1];
       v += texelFetch(u_value, fc + ivec2(-1,  1), 0) * weight[2];
       v += texelFetch(u_value, fc + ivec2( 0, -1), 0) * weight[3];
       v += texelFetch(u_value, fc + ivec2( 0,  0), 0) * weight[4];
       v += texelFetch(u_value, fc + ivec2( 0,  1), 0) * weight[5];
       v += texelFetch(u_value, fc + ivec2( 1, -1), 0) * weight[6];
       v += texelFetch(u_value, fc + ivec2( 1,  0), 0) * weight[7];
       v += texelFetch(u_value, fc + ivec2( 1,  1), 0) * weight[8];
       fragColor = v;
}
        </script>
        <script id="debugPatch.vert" type="x-shader/x-vertex">#version 300 es
in vec2 a_position;

out vec2 v_coord;

void main(void) {
    v_coord = (a_position / 2.0) + 0.5;
    gl_Position = vec4(a_position, 0, 1.0);
}
        </script>

        <script id="debugPatch.frag" type="x-shader/x-fragment">#version 300 es
precision mediump float;
uniform sampler2D u_value;

out vec4 fragColor;

void main(void) {
    ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
    fragColor = texelFetch(u_value, fc, 0) * 100.0;
}
        </script>

	</head>
	<body>
		<div>
                   <div style="float: left">
                     <canvas id="canvas"></canvas>
                   </div>
		   <div id="code"></div>
                </div>
                <br>
		 <div style="clear: left" id="readout"</div>

<!--		<canvas id="debugCanvas1"></canvas> -->
	</body>
</html>
