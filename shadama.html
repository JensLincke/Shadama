<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
        <title>Shadama2</title>
        <link href="shadama.css" rel="stylesheet">
        <link rel="stylesheet" href="codemirror/codemirror.css">
        <script src="codemirror/codemirror.js"></script>
        <script src="ohm/ohm.min.js"></script>
        <script src="shadama-lang.js"></script>
        <script src="shadama.js" type="text/javascript"></script>
        <script src="shadama-tests.js" type="text/javascript"></script>
        <script type="text/ohm-js">
Shadama {
  TopLevel
    = ProgramDecl? (Breed | Patch | Script | Static)*

  ProgramDecl = program string
  Breed = breed ident "(" Formals ")"
  Patch = patch ident "(" Formals ")"
  Script = def ident "(" Formals ")" Block
  Static = static ident "(" Formals ")" Block

  Formals
    = ident ("," ident)* -- list
    | empty

  Block = "{" StatementList "}"

  StatementList = Statement*

  Statement
    = Block
    | VariableStatement
    | AssignmentStatement
    | ExpressionStatement
    | IfStatement
    | ExpressionStatement

  VariableStatement = var VariableDeclaration ";"
  VariableDeclaration = ident Initialiser?
  Initialiser = "=" Expression

  ExpressionStatement = Expression ";"
  IfStatement = if "(" Expression ")" Statement (else Statement)?

  AssignmentStatement
    = LeftHandSideExpression "=" Expression ";"

  LeftHandSideExpression
    = ident "." ident -- field
    | ident

  Expression = EqualityExpression

  EqualityExpression
    = EqualityExpression "==" LogicalExpression  -- equal
    | EqualityExpression "!=" LogicalExpression  -- notEqual
    | LogicalExpression

  LogicalExpression
    = LogicalExpression "&&" RelationalExpression       -- and
    | LogicalExpression "||" RelationalExpression       -- or
    | RelationalExpression

  RelationalExpression
    = RelationalExpression "<" AddExpression           -- lt
    | RelationalExpression ">" AddExpression           -- gt
    | RelationalExpression "<=" AddExpression          -- le
    | RelationalExpression ">=" AddExpression          -- ge
    | AddExpression

  AddExpression
    = AddExpression "+" MulExpression  -- plus
    | AddExpression "-" MulExpression -- minus
    | MulExpression

  MulExpression
    = MulExpression "*" PrimExpression  -- times
    | MulExpression "/" PrimExpression  -- divide
    | UnaryExpression

  UnaryExpression
    = "+" PrimExpression -- plus
    | "-" PrimExpression -- minus
    | PrimExpression

  PrimExpression
    = "(" Expression ")"  -- paren
    | PrimitiveCall
    | MethodCall
    | PrimExpression "." ident     -- field
    | ident               -- variable
    | string              -- string
    | number              -- number

  PrimitiveCall
    = ident "(" Actuals ")"

  MethodCall
    = ident "." ident "(" Actuals ")"

  Actuals
    = Expression ("," Expression)* -- list
    | empty

  ident
    = letter (alnum | "_")*

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole

  string = "\"" doubleStringCharacter* "\""

  doubleStringCharacter 
    = "\\" any           -- escaped
    | ~"\"" any          -- nonEscaped

  identifierStart = letter | "_"
  identifierPart = identifierStart | digit

  var = "var" ~identifierPart
  if = "if" ~identifierPart
  breed = "breed" ~identifierPart
  patch = "patch" ~identifierPart
  else = "else" ~identifierPart
  def = "def" ~identifierPart
  this = "this" ~identifierPart
  self = "self" ~identifierPart
  static = "static" ~identifierPart
  program = "program" ~identifierPart

  empty =
  space
   += "//" (~nl any)* nl  -- cppComment
    | "/*" (~"*/" any)* "*/" -- cComment
  nl = "\n"
}
    </script>

        <script id="copy.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="copy.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_value;

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  fragColor = texelFetch(u_value, fc, 0).r;
}
        </script>


        <script id="drawBreed.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_x;
uniform sampler2D u_y;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 v_color;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    float x = texelFetch(u_x, ivec2(a_index), 0).r;
    float y = texelFetch(u_y, ivec2(a_index), 0).r;
    vec2 dPos = vec2(x, y);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)
    gl_Position = vec4(clipPos, 0, 1.0);

    float r = texelFetch(u_r, ivec2(a_index), 0).r;
    float g = texelFetch(u_g, ivec2(a_index), 0).r;
    float b = texelFetch(u_b, ivec2(a_index), 0).r;
    float a = texelFetch(u_a, ivec2(a_index), 0).r;
    v_color = vec4(r, g, b, a);
    gl_PointSize = 1.0;
}
        </script>

        <script id="drawBreed.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec4 v_color;

out vec4 fragColor;

void main(void) {
  fragColor = v_color;
}
        </script>

        <script id="drawPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="drawPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float r = texelFetch(u_r, fc, 0).r;
  float g = texelFetch(u_g, fc, 0).r;
  float b = texelFetch(u_b, fc, 0).r;
  float a = texelFetch(u_a, fc, 0).r;
  fragColor = vec4(r, g, b, a);
}
        </script>
        <script id="diffusePatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="diffusePatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

const float weight[9] = float[9](
    0.077847, 0.123317, 0.077847,
    0.123317, 0.195346, 0.123317,
    0.077847, 0.123317, 0.077847
);

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float v;
  v = texelFetch(u_value, fc + ivec2(-1, -1), 0).r * weight[0];
  v += texelFetch(u_value, fc + ivec2(-1,  0), 0).r * weight[1];
  v += texelFetch(u_value, fc + ivec2(-1,  1), 0).r * weight[2];
  v += texelFetch(u_value, fc + ivec2( 0, -1), 0).r * weight[3];
  v += texelFetch(u_value, fc + ivec2( 0,  0), 0).r * weight[4];
  v += texelFetch(u_value, fc + ivec2( 0,  1), 0).r * weight[5];
  v += texelFetch(u_value, fc + ivec2( 1, -1), 0).r * weight[6];
  v += texelFetch(u_value, fc + ivec2( 1,  0), 0).r * weight[7];
  v += texelFetch(u_value, fc + ivec2( 1,  1), 0).r * weight[8];
  v = v <= (1.0/256.0) ? 0.0 : v;
  fragColor = v;
}
        </script>
        <script id="debugPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="debugPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

out vec4 fragColor;

void main(void) {
    ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
    fragColor = texelFetch(u_value, fc, 0);
}
        </script>

        <script id="forward.shadama" type="x-shadama">
program "Duck Bounce!"
breed Turtle (x, y, dx, dy, r, g, b, a, origX, origY)
breed Filler (x, y)
patch Field (nx, ny, r, g, b, a)

def setColor() {
  this.r = this.x / 256.0;
  this.g = this.y / 256.0;
  this.b = 0.0;
  this.a = 1.0;
}

def clear(field) {
  field.r = 0.0;
  field.g = 0.0;
  field.b = 0.0;
  field.a = 0.0;
  field.nx = 0.0;
  field.ny = 0.0;
}

def remember() {
  this.origX = this.x;
  this.origY = this.y;
}

def fillCircle(cx, cy, r, field) {
  var dx = this.x - cx;
  var dy = this.y - cy;
  var dr = sqrt(dx * dx + dy * dy);
  if (dr < r) {
    field.r = 0.2;
    field.g = 0.2;
    field.b = 0.8;
    field.a = 1.0;
    field.nx = dx / r;
    field.ny = dy / r;
  }
}

def maybeMove() {
  var dx = this.dx;
  var dy = this.dy;
  this.x = this.x + dx;
  this.y = this.y + dy;
  this.dy = dy - 0.01;
}

def fillD() {
  this.dx = 0.0;
  this.dy = 0.0;
}
 
def bounce(field, time) {
  var nx = field.nx;
  var ny = field.ny;
  var dx = this.dx;
  var dy = this.dy - 0.01;
  var dot = dx * nx + dy * ny;
  var rx = dx;
  var ry = dy;

  if (dot < 0.0) {
    rx = dx - 2.0 * dot * nx;
    ry = dy - 2.0 * dot * ny;
  }

  var norm = sqrt(rx * rx + ry * ry);
  var normX = rx / norm;
  var normY = ry / norm;

  var newX = this.x + dx;
  var newY = this.y + dy;

  if (newX < 0.0) {
    newX = -newX;
    rx = -rx * 0.9;
  }
  if (newX > u_resolution.x) {
    newX = u_resolution.x - (newX - u_resolution.x);
    rx = -rx * 0.9;
  }
  if (newY < 0.0) {
    newY = mod(newY, u_resolution.y);
    ry = -0.1;
  }
  if (newY > u_resolution.y) {
    newY = u_resolution.y - (newY - u_resolution.y);
    ry = -ry;
  }

  if (mod(floor(time), 10.0) == 0.0) {
    this.x = this.origX;
    this.y = this.origY;
  } else {
    this.x = newX;
    this.y = newY;
  }
  this.dx = rx;
  this.dy = ry;
}

static setup() {
  Filler.fillSpace("x", "y", 512, 512);
  Turtle.fillImage("x", "y", "r", "g", "b", "a", image);
  Turtle.remember();
  Turtle.fillD();
}

static loop() {
  Filler.clear(Field);
  Filler.fillCircle(mousemove.x, mousemove.y, 20, Field);
  Turtle.bounce(Field, time);
  Field.draw();
  Turtle.draw();
}
</script>

<style>
.CodeMirror {
  float: left;
  margin-right: 10px;
}

.staticName {
  user-select: none;
  display: inline-block;
  border: solid #444444 1px;
}

.staticName:hover, .staticName:focus {
  background-color: #cccccc;
}

/* Dropdown Button */
.dropbtn {
    background-color: #4CAF50;
    color: white;
    padding: 10px;
    font-size: 13px;
    border: none;
    margin-bottom: 10px;
    cursor: pointer;
}

/* Dropdown button on hover & focus */
.dropbtn:hover, .dropbtn:focus {
    background-color: #3e8e41;
}

/* The container <div> - needed to position the dropdown content */
.dropdown {
    position: relative;
    display: inline-block;
}

/* Dropdown Content (Hidden by Default) */
.dropdown-content {
    display: none;
    position: absolute;
    background-color: #f9f9f9;
    min-width: 160px;
    box-shadow: 0px 8px 16px 0px rgba(0,0,0,0.2);
    z-index: 1;
}

/* Links inside the dropdown */
.dropdown-content a {
    color: black;
    padding: 2px 2px;
    text-decoration: none;
    display: block;
}

/* Change color of dropdown links on hover */
.dropdown-content a:hover {background-color: #f1f1f1}

/* Show the dropdown menu (use JS to add this class to the .dropdown-content container when the user clicks on the dropdown button) */
.show {display:block;}

</style>

        </head>
        <body>
          <h1 id="bigTitle">Shadama</h1>
                <div style="float: left">
                      <canvas id="canvas"></canvas>
                </div>
                <textarea style="float: left" id="code"></textarea>
                <div style="float: left" id="controlBox">
 		  <div class="dropdown" id="programs">
                     <button onclick="toggleFileList()" class="dropbtn">Programs</button>
                     <div id="myDropdown" class="dropdown-content">
                     </div>
                  </div>

		  <hr>
 		  <div id="watcherList"></div>
                </div>
                <br>
                <div style="clear: left" id="readout"></div>
                <canvas id="debugCanvas1"></canvas>
        </body>
</html>
