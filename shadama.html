<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
        <title>Shadama2</title>
        <link href="shadama.css" rel="stylesheet">
        <link rel="stylesheet" href="codemirror/codemirror.css">
        <script src="codemirror/codemirror.js"></script>
        <script src="codemirror/clike.js"></script>
        <script src="ohm/ohm.min.js"></script>
        <script src="shadama-lang.js"></script>
        <script src="shadama.js" type="text/javascript"></script>
        <script src="shadama-tests.js" type="text/javascript"></script>
        <script type="text/ohm-js">
Shadama {
  TopLevel
    = ProgramDecl? (Breed | Patch | Script | Static)*

  ProgramDecl = program string
  Breed = breed ident "(" Formals ")"
  Patch = patch ident "(" Formals ")"
  Script = def ident "(" Formals ")" Block
  Static = static ident "(" Formals ")" Block

  Formals
    = ident ("," ident)* -- list
    | empty

  Block = "{" StatementList "}"

  StatementList = Statement*

  Statement
    = Block
    | VariableStatement
    | AssignmentStatement
    | ExpressionStatement
    | IfStatement
    | ExpressionStatement

  VariableStatement = var VariableDeclaration ";"
  VariableDeclaration = ident Initialiser?
  Initialiser = "=" Expression

  ExpressionStatement = Expression ";"
  IfStatement = if "(" Expression ")" Statement (else Statement)?

  AssignmentStatement
    = LeftHandSideExpression "=" Expression ";"

  LeftHandSideExpression
    = ident "." ident -- field
    | ident

  Expression = EqualityExpression

  EqualityExpression
    = EqualityExpression "==" LogicalExpression  -- equal
    | EqualityExpression "!=" LogicalExpression  -- notEqual
    | LogicalExpression

  LogicalExpression
    = LogicalExpression "&&" RelationalExpression       -- and
    | LogicalExpression "||" RelationalExpression       -- or
    | RelationalExpression

  RelationalExpression
    = RelationalExpression "<" AddExpression           -- lt
    | RelationalExpression ">" AddExpression           -- gt
    | RelationalExpression "<=" AddExpression          -- le
    | RelationalExpression ">=" AddExpression          -- ge
    | AddExpression

  AddExpression
    = AddExpression "+" MulExpression  -- plus
    | AddExpression "-" MulExpression -- minus
    | MulExpression

  MulExpression
    = MulExpression "*" PrimExpression  -- times
    | MulExpression "/" PrimExpression  -- divide
    | MulExpression "%" PrimExpression  -- mod
    | UnaryExpression

  UnaryExpression
    = "+" PrimExpression -- plus
    | "-" PrimExpression -- minus
    | "!" PrimExpression -- not
    | PrimExpression

  PrimExpression
    = "(" Expression ")"  -- paren
    | PrimitiveCall
    | MethodCall
    | PrimExpression "." ident     -- field
    | ident               -- variable
    | string              -- string
    | number              -- number

  PrimitiveCall
    = ident "(" Actuals ")"

  MethodCall
    = ident "." ident "(" Actuals ")"

  Actuals
    = Expression ("," Expression)* -- list
    | empty

  ident
    = letter (alnum | "_")*

  number
    = digit* "." digit+  -- fract
    | digit+             -- whole

  string = "\"" doubleStringCharacter* "\""

  doubleStringCharacter
    = "\\" any           -- escaped
    | ~"\"" any          -- nonEscaped

  identifierStart = letter | "_"
  identifierPart = identifierStart | digit

  var = "var" ~identifierPart
  if = "if" ~identifierPart
  breed = "breed" ~identifierPart
  patch = "patch" ~identifierPart
  else = "else" ~identifierPart
  def = "def" ~identifierPart
  this = "this" ~identifierPart
  self = "self" ~identifierPart
  static = "static" ~identifierPart
  program = "program" ~identifierPart

  empty =
  space
   += "//" (~nl any)* nl  -- cppComment
    | "/*" (~"*/" any)* "*/" -- cComment
  nl = "\n"
}
    </script>

        <script id="copy.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="copy.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_value;

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  fragColor = texelFetch(u_value, fc, 0).r;
}
        </script>


        <script id="drawBreed.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;

uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_x;
uniform sampler2D u_y;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 v_color;

void main(void) {
    vec2 zeroToOne = a_index / u_particleLength;
    float x = texelFetch(u_x, ivec2(a_index), 0).r;
    float y = texelFetch(u_y, ivec2(a_index), 0).r;
    vec2 dPos = vec2(x, y);   // (0-resolution, 0-resolution)
    vec2 normPos = dPos / u_resolution;  // (0-1.0, 0-1.0)
    vec2 clipPos = normPos * 2.0 - 1.0;  // (-1.0-1.0, -1.0-1.0)
    gl_Position = vec4(clipPos, 0, 1.0);

    float r = texelFetch(u_r, ivec2(a_index), 0).r;
    float g = texelFetch(u_g, ivec2(a_index), 0).r;
    float b = texelFetch(u_b, ivec2(a_index), 0).r;
    float a = texelFetch(u_a, ivec2(a_index), 0).r;
    v_color = vec4(r, g, b, a);
    gl_PointSize = 1.0;
}
        </script>

        <script id="drawBreed.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

in vec4 v_color;

out vec4 fragColor;

void main(void) {
  fragColor = v_color;
}
        </script>

        <script id="drawPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="drawPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;

uniform sampler2D u_r;
uniform sampler2D u_g;
uniform sampler2D u_b;
uniform sampler2D u_a;

out vec4 fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float r = texelFetch(u_r, fc, 0).r;
  float g = texelFetch(u_g, fc, 0).r;
  float b = texelFetch(u_b, fc, 0).r;
  float a = texelFetch(u_a, fc, 0).r;
  fragColor = vec4(r, g, b, a);
}
        </script>
        <script id="diffusePatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="diffusePatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

const float weight[9] = float[9](
    0.077847, 0.123317, 0.077847,
    0.123317, 0.195346, 0.123317,
    0.077847, 0.123317, 0.077847
);

out float fragColor;

void main(void) {
  ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
  float v;
  v = texelFetch(u_value, fc + ivec2(-1, -1), 0).r * weight[0];
  v += texelFetch(u_value, fc + ivec2(-1,  0), 0).r * weight[1];
  v += texelFetch(u_value, fc + ivec2(-1,  1), 0).r * weight[2];
  v += texelFetch(u_value, fc + ivec2( 0, -1), 0).r * weight[3];
  v += texelFetch(u_value, fc + ivec2( 0,  0), 0).r * weight[4];
  v += texelFetch(u_value, fc + ivec2( 0,  1), 0).r * weight[5];
  v += texelFetch(u_value, fc + ivec2( 1, -1), 0).r * weight[6];
  v += texelFetch(u_value, fc + ivec2( 1,  0), 0).r * weight[7];
  v += texelFetch(u_value, fc + ivec2( 1,  1), 0).r * weight[8];
  v = v <= (1.0/256.0) ? 0.0 : v;
  fragColor = v;
}
        </script>

        <script id="debugPatch.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_position;

void main(void) {
    gl_Position = vec4(a_position, 0.0, 1.0);
}
        </script>

        <script id="debugPatch.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
uniform sampler2D u_value;

out vec4 fragColor;

void main(void) {
    ivec2 fc = ivec2(gl_FragCoord.s, gl_FragCoord.t);
    fragColor = texelFetch(u_value, fc, 0);
}
        </script>

        <script id="debugFrame.vert" type="x-shader/x-vertex">#version 300 es
layout (location = 0) in vec2 a_index;
uniform vec2 u_resolution;
uniform float u_particleLength;
uniform sampler2D u_this_r;
uniform sampler2D u_this_g;
uniform sampler2D u_this_b;
uniform sampler2D u_this_a;
out float v_this_r;
out float v_this_g;
out float v_this_b;
out float v_this_a;
void main() {

  vec2 oneToOne = (a_index / u_particleLength) * 2.0 - 1.0;
  v_this_r = texelFetch(u_this_r, ivec2(a_index), 0).r;
  v_this_g = texelFetch(u_this_g, ivec2(a_index), 0).r;
  v_this_b = texelFetch(u_this_b, ivec2(a_index), 0).r;
  v_this_a = texelFetch(u_this_a, ivec2(a_index), 0).r;
  v_this_r = 1.0;
  v_this_g = 0.0;
  v_this_b = 1.0;
  v_this_a = 1.0;

  gl_Position = vec4(oneToOne, 0.0, 1.0);
  gl_PointSize = 1.0;
}

        </script>
        <script id="debugFrame.frag" type="x-shader/x-fragment">#version 300 es
precision highp float;
in float v_this_r;
in float v_this_g;
in float v_this_b;
in float v_this_a;
layout (location = 0) out float o_this_r;
layout (location = 1) out float o_this_g;
layout (location = 2) out float o_this_b;
layout (location = 3) out float o_this_a;
void main() {
  o_this_r = v_this_r;
  o_this_g = v_this_g;
  o_this_b = v_this_b;
  o_this_a = v_this_a;
}
        </script>
        <script id="forward.shadama" type="x-shadama">
program "Life"
breed Turtle (n, x, y, c)
patch Field (c, r, g, b, a)

def reset(field) {
  var rand = random(this.x * 1233.1 + this.y * 0.2);
  if (rand > 0.3) {
    field.c = 1;
  } else {
    field.c = 0;
  }
}

def put(field, x, y) {
  var dx = this.x - x;
  var dy = this.y - y;
  if ((dx * dx + dy * dy) < 2) {
    field.c = 1;
  }
}

def add(field) {
  this.c = this.c + field.c;
}

def move(x, y) {
  this.x = this.x + x;
  this.y = this.y + y;
}

def zero(field) {
  this.c = 0;
  this.n = field.c;
}

def comp(field) {
  var c = this.c;
  var n = this.n;
  if (n == 0) {
    if (c == 3) {
      field.c = 1;
    } else {
      field.c = 0;
    }
  } else {
    if ((c == 3) || (c == 2)) {
       field.c = 1;
    } else {
       field.c = 0;
    }
  }   
}

def color(field) {
  field.b = 0.0;
  field.a = 1.0;
  if (field.c > 0) {
    field.r = 1.0;
    field.g = 1.0;
  } else {
    field.r = 0.0;
    field.g = 0.0;
  }
}

static setup(env) {
  Turtle.fillSpace("x", "y", width, height);
  Turtle.reset(Field);
  Turtle.color(Field);
  Field.draw();
}

static loop() {
  Turtle.put(Field, mousemove.x, mousemove.y);
  Turtle.zero(Field);
  Turtle.move(-1, 1);
  Turtle.add(Field);

  Turtle.move(1, 0);
  Turtle.add(Field);
  Turtle.move(1, 0);
  Turtle.add(Field);

  Turtle.move(-2, -1);
  Turtle.add(Field);
  Turtle.move(1, 0);
//  Turtle.add(Field);

  Turtle.move(1, 0);
  Turtle.add(Field);
  Turtle.move(-2, -1);
  Turtle.add(Field);
  Turtle.move(1, 0);
  Turtle.add(Field);
  Turtle.move(1, 0);
  Turtle.add(Field);

  Turtle.move(-1, 1);

  Turtle.comp(Field);
  Turtle.color(Field);
  Field.draw();
}
</script>
        </head>
        <body>
          <h1 id="bigTitle">Shadama</h1>
                <div style="float: left">
                      <canvas id="shadamaCanvas"></canvas>
                </div>
                <textarea style="float: left" id="code"></textarea>
                <div style="float: left" id="controlBox">
                  <select id="myDropdown" class="dropdown-content">
                  </select>
                  <hr>
                  <div id="watcherList"></div>
                  <hr>
                  <div id="envList"></div>
                </div>
                <br>
                <div style="clear: left" id="readout"></div>
                <canvas id="debugCanvas1"></canvas>
        </body>
</html>
